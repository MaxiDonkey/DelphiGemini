unit Gemini.Exceptions;

interface

uses
  System.SysUtils, System.Classes, System.Net.HttpClient, System.Net.URLClient,
  System.Net.Mime, System.JSON, Gemini.Errors;

type
  GeminiException = class(Exception)
  private
    FCode: Int64;
    FMsg: string;
    FStatus: string;
  public
    constructor Create(const ACode: Int64; const AError: TErrorCore); reintroduce; overload;
    constructor Create(const ACode: Int64; const Value: string); reintroduce; overload;
    property Code: Int64 read FCode write FCode;
    property Msg: string read FMsg write FMsg;
    property Status: string read FStatus write FStatus;
  end;

  /// <summary>
  /// The `GeminiExceptionAPI` class represents a generic API-related exception.
  /// It is thrown when there is an issue with the API configuration or request process,
  /// such as a missing API token, invalid base URL, or other configuration errors.
  /// This class serves as a base for more specific API exceptions.
  /// </summary>
  GeminiExceptionAPI = class(Exception);

  /// <summary>
  /// The request body is malformed.
  /// <para>
  /// There is a typo, or a missing required field in your request.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Check the API reference for request format, examples, and supported versions. Using features from a newer API version with an older endpoint can cause errors.
  /// </remarks>
  EInvalidArgument = class(GeminiException);

  /// <summary>
  /// The API key doesn't have the required permissions.
  /// <para>
  /// Wrong API key is used; trying to use a tuned model without going through proper authentication.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Check that the API key is set and has the right access. And make sure to go through proper authentication to use tuned models.
  /// </remarks>
  EPermissionDenied = class(GeminiException);

  /// <summary>
  /// The requested resource wasn't found.
  /// <para>
  /// An image, audio, or video file referenced in your request was not found.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Check if all parameters in your request are valid for your API version.
  /// </remarks>
  EResourceNotFound = class(GeminiException);

  /// <summary>
  /// The rate limit have been exceeded.
  /// <para>
  /// Too many requests per minute with the free tier Gemini API.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Ensure you're within the model's rate limit. Request a quota increase if needed.
  /// </remarks>
  EResourceExhausted = class(GeminiException);

  /// <summary>
  /// An unexpected error occurred on Google's side.
  /// <para>
  /// The input context is too long.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Reduce your input context or temporarily switch to another model (e.g. from Gemini 1.5 Pro to Gemini 1.5 Flash) and see if it works. Or wait a bit and retry your request. If the issue persists after retrying, please report it using the Send feedback button in Google AI Studio.
  /// </remarks>
  EInternalError = class(GeminiException);

  /// <summary>
  /// The service may be temporarily overloaded or down.
  /// <para>
  /// The service is temporarily running out of capacity.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Temporarily switch to another model (e.g. from Gemini 1.5 Pro to Gemini 1.5 Flash) and see if it works. Or wait a bit and retry your request. If the issue persists after retrying, please report it using the Send feedback button in Google AI Studio.
  /// </remarks>
  ETryAgain = class(GeminiException);

  /// <summary>
  /// The service is unable to finish processing within the deadline.
  /// <para>
  /// The prompt (or context) is too large to be processed in time.
  /// </para>
  /// </summary>
  /// <remarks>
  /// Set a larger 'timeout' in your client request to avoid this error.
  /// </remarks>
  EDeadlineExceeded = class(GeminiException);

  /// <summary>
  /// An `InvalidResponse` error occurs when the API response is either empty or not in the expected format.
  /// This error indicates that the API did not return a valid response that can be processed, possibly due to a server-side issue,
  /// a malformed request, or unexpected input data.
  /// </summary>
  /// <remarks>
  /// Check if all parameters in your request are valid for your API version.
  /// </remarks>
  GeminiExceptionInvalidResponse = class(GeminiException);

implementation

{ GeminiException }

constructor GeminiException.Create(const ACode: Int64; const AError: TErrorCore);
begin
  Code := ACode;
  Msg := (AError as TError).Error.Message;
  Status := (AError as TError).Error.Status;

  inherited Create(Format('error (%d) %s: '+ sLineBreak + '     %s', [Code, Status, Msg]));
end;

constructor GeminiException.Create(const ACode: Int64; const Value: string);
begin
  Code := ACode;
  Msg := Value;
  inherited Create(Format('error %d: %s', [ACode, Msg]));
end;

end.
