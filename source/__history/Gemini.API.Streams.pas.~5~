unit Gemini.API.Streams;

interface

uses
  System.SysUtils, System.Classes, System.SyncObjs;

type
  TLockedMemoryStream = class(TMemoryStream)
  private
    FLock: TCriticalSection;
  public
    constructor Create;
    destructor Destroy; override;

    function Write(const Buffer; Count: Longint): Longint; override;

    function ExtractDelta(var Offset: Int64; out Bytes: TBytes): Boolean;
  end;

implementation

{ TLockedMemoryStream }

constructor TLockedMemoryStream.Create;
begin
  inherited Create;
  FLock := TCriticalSection.Create;
end;

destructor TLockedMemoryStream.Destroy;
begin
  FLock.Free;
  inherited;
end;

function TLockedMemoryStream.Write(const Buffer; Count: Longint): Longint;
begin
  FLock.Enter;
  try
    Result := inherited Write(Buffer, Count);
  finally
    FLock.Leave;
  end;
end;

function TLockedMemoryStream.ExtractDelta(var Offset: Int64; out Bytes: TBytes): Boolean;
var
  NewCount: Int64;
begin
  Result := False;
  SetLength(Bytes, 0);

  FLock.Enter;
  try
    NewCount := Size - Offset;
    if (Offset < 0) or (Offset > Size) or (NewCount <= 0) then
      Exit;

    SetLength(Bytes, NewCount);
    Move(PByte(Memory)[NativeInt(Offset)], Bytes[0], NewCount);
    Offset := Offset + NewCount;

    Result := True;
  finally
    FLock.Leave;
  end;
end;


end.
