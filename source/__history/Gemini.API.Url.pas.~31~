unit Gemini.API.Url;

interface

uses
  System.SysUtils;

type
  TGeminiUrl = record
  private
    FBaseUrl: string;
    FVersion: string;
    FToken: string;

    class function TrimSlashes(const Value: string): string; static;
    class function JoinPath(const Left, Right: string): string; static;
    class function AppendQuery(const Url, Query: string): string; static;
  public
    class function Create(const BaseUrl, Version, Token: string): TGeminiUrl; static;

    function FilesUrl(const Path: string): string;
    function RequestUrl(const Path: string): string; overload;
    function RequestUrl(const Path, Params: string): string; overload;
    function RequestFilesUrl(const Path: string): string;
    function PatchUrl(const Path, UpdateMask: string): string;
  end;

implementation

{ TGeminiUrl }

class function TGeminiUrl.Create(const BaseUrl, Version, Token: string): TGeminiUrl;
begin
  Result.FBaseUrl := BaseUrl;
  Result.FVersion := Version;
  Result.FToken := Token;
end;

class function TGeminiUrl.TrimSlashes(const Value: string): string;
begin
  Result := Value.Trim.Trim(['/']);
end;

class function TGeminiUrl.JoinPath(const Left, Right: string): string;
begin
  var LLeft := left.TrimRight(['/']);
  var LRight := right.TrimLeft(['/']);

  if LLeft.IsEmpty then
    Exit(LRight);

  if LRight.IsEmpty
    then Exit(LLeft);

  Result := LLeft + '/' + LRight;
end;

class function TGeminiUrl.AppendQuery(const Url, Query: string): string;
begin
  var LQuery := Query.Trim;

  if LQuery.IsEmpty then
    Exit(Url);

  if LQuery.StartsWith('?') then
    LQuery := LQuery.Substring(1);

  if LQuery.StartsWith('&') then
    LQuery := LQuery.Substring(1);

  if url.Contains('?') then
    Result := Url + '&' + LQuery
  else
    Result := Url + '?' + LQuery;
end;

function TGeminiUrl.FilesUrl(const Path: string): string;
begin
  Result := JoinPath(FBaseUrl, TrimSlashes(Path));
end;

function TGeminiUrl.RequestUrl(const Path: string): string;
begin
  var Url := JoinPath(JoinPath(FBaseUrl, FVersion), TrimSlashes(Path));
  Result := AppendQuery(Url, 'key=' + FToken);
end;

function TGeminiUrl.RequestUrl(const Path, Params: string): string;
begin
  var Url := JoinPath(JoinPath(FBaseUrl, FVersion), TrimSlashes(Path));
  Result := AppendQuery(Url, 'key=' + FToken);
  Result := AppendQuery(Result, Params);
end;

function TGeminiUrl.RequestFilesUrl(const Path: string): string;
begin
  var Url := JoinPath(FBaseUrl, TrimSlashes(Path));
  Result := AppendQuery(Url, 'key=' + FToken);
end;

function TGeminiUrl.PatchUrl(const Path, UpdateMask: string): string;
begin
  var Url := JoinPath(JoinPath(FBaseUrl, FVersion), TrimSlashes(Path));
  Result := AppendQuery(Url, 'key=' + FToken);
  Result := AppendQuery(Result, 'updateMask=' + UpdateMask);
end;

end.
