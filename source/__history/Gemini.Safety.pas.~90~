unit Gemini.Safety;

{-------------------------------------------------------------------------------

      Github repository :  https://github.com/MaxiDonkey/DelphiGemini
      Visit the Github repository for the documentation and use examples

 ------------------------------------------------------------------------------}

interface

uses
  System.SysUtils, System.Classes, REST.JsonReflect, System.JSON, REST.Json.Types,
  Gemini.API.Params, Gemini.Types;

type
  /// <summary>
  /// Represents a safety setting that affects the safety-blocking behavior.
  /// </summary>
  /// <remarks>
  /// By setting safety configurations for specific categories, you can adjust the allowed probability that content is blocked based on harm categories.
  /// </remarks>
  TSafety = record
  private
    FCategory: THarmCategory;
    FThreshold: THarmBlockThreshold;
  public
    /// <summary>
    /// Sets the category for this safety setting.
    /// </summary>
    /// <param name="Value">
    /// The harm category to set.
    /// </param>
    /// <returns>
    /// The updated <c>TSafety</c> record with the specified category.
    /// </returns>
    function Category(Value: THarmCategory): TSafety;

    /// <summary>
    /// Sets the probability threshold at which harm is blocked for this safety setting.
    /// </summary>
    /// <param name="Value">
    /// The harm block threshold to set.
    /// </param>
    /// <returns>
    /// The updated <c>TSafety</c> record with the specified threshold.
    /// </returns>
    function Threshold(Value: THarmBlockThreshold): TSafety;

    /// <summary>
    /// Converts this <c>TSafety</c> record to a JSON object.
    /// </summary>
    /// <returns>
    /// A <c>TJSONObject</c> representing this safety setting.
    /// </returns>
    function ToJson: TJSONObject;

    /// <summary>
    /// Creates a new <c>TSafety</c> record with the specified category and threshold.
    /// </summary>
    /// <param name="Category">
    /// The harm category to set.
    /// </param>
    /// <param name="Threshold">
    /// The harm block threshold to set.
    /// </param>
    /// <returns>
    /// A new <c>TSafety</c> record with the specified category and threshold.
    /// </returns>
    class function New(Category: THarmCategory; Threshold: THarmBlockThreshold): TSafety; static;

    /// <summary>
    /// Returns an array of <c>TSafety</c> settings that do not block any content.
    /// </summary>
    /// <returns>
    /// An array of <c>TSafety</c> records with all categories set to <c>BLOCK_NONE</c>.
    /// </returns>
    class function DontBlock: TArray<TSafety>; static;

    /// <summary>
    /// Creates a <c>TSafety</c> setting for the Sexually Explicit category with the specified threshold.
    /// </summary>
    /// <param name="Value">
    /// The harm block threshold to set for the Sexually Explicit category.
    /// </param>
    /// <returns>
    /// A <c>TSafety</c> record for the Sexually Explicit category with the specified threshold.
    /// </returns>
    class function SexuallyExplicit(const Value: THarmBlockThreshold): TSafety; static;

    /// <summary>
    /// Creates a <c>TSafety</c> setting for the Hate Speech category with the specified threshold.
    /// </summary>
    /// <param name="Value">
    /// The harm block threshold to set for the Hate Speech category.
    /// </param>
    /// <returns>
    /// A <c>TSafety</c> record for the Hate Speech category with the specified threshold.
    /// </returns>
    class function HateSpeech(const Value: THarmBlockThreshold): TSafety; static;

    /// <summary>
    /// Creates a <c>TSafety</c> setting for the Harassment category with the specified threshold.
    /// </summary>
    /// <param name="Value">
    /// The harm block threshold to set for the Harassment category.
    /// </param>
    /// <returns>
    /// A <c>TSafety</c> record for the Harassment category with the specified threshold.
    /// </returns>
    class function Harassment(const Value: THarmBlockThreshold): TSafety; static;

    /// <summary>
    /// Creates a <c>TSafety</c> setting for the Dangerous Content category with the specified threshold.
    /// </summary>
    /// <param name="Value">
    /// The harm block threshold to set for the Dangerous Content category.
    /// </param>
    /// <returns>
    /// A <c>TSafety</c> record for the Dangerous Content category with the specified threshold.
    /// </returns>
    class function DangerousContent(const Value: THarmBlockThreshold): TSafety; static;

    class function Derogatory(const Value: THarmBlockThreshold): TSafety; static;

    class function Toxicity(const Value: THarmBlockThreshold): TSafety; static;

    class function Violence(const Value: THarmBlockThreshold): TSafety; static;

    class function Medical(const Value: THarmBlockThreshold): TSafety; static;
  end;

implementation

uses
  System.StrUtils, System.Rtti, Rest.Json;

{ TSafety }

function TSafety.Category(Value: THarmCategory): TSafety;
begin
  FCategory := Value;
  Result := Self;
end;

class function TSafety.DangerousContent(
  const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_DANGEROUS_CONTENT, Value);
end;

class function TSafety.Derogatory(const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_DEROGATORY, Value);
end;

class function TSafety.DontBlock: TArray<TSafety>;
begin
  Result := [
    SexuallyExplicit(BLOCK_NONE),
    HateSpeech(BLOCK_NONE),
    Harassment(BLOCK_NONE),
    DangerousContent(BLOCK_NONE),
    Derogatory(BLOCK_NONE),
    Toxicity(BLOCK_NONE),
    Violence(BLOCK_NONE),
    Medical(BLOCK_NONE)
  ];
end;

class function TSafety.Harassment(
  const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_HARASSMENT, Value);
end;

class function TSafety.HateSpeech(
  const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_HATE_SPEECH, Value);
end;

class function TSafety.Medical(const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_MEDICAL, Value);
end;

class function TSafety.New(Category: THarmCategory;
  Threshold: THarmBlockThreshold): TSafety;
begin
  Result := Result.Category(Category).Threshold(Threshold);
end;

class function TSafety.SexuallyExplicit(
  const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_SEXUALLY_EXPLICIT, Value);
end;

function TSafety.Threshold(Value: THarmBlockThreshold): TSafety;
begin
  FThreshold := Value;
  Result := Self;
end;

function TSafety.ToJson: TJSONObject;
begin
  Result := TJSONObject.Create
    .AddPair('category', FCategory.ToString)
    .AddPair('threshold', FThreshold.ToString);
end;

class function TSafety.Toxicity(const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_TOXICITY, Value);
end;

class function TSafety.Violence(const Value: THarmBlockThreshold): TSafety;
begin
  Result := TSafety.New(HARM_CATEGORY_VIOLENCE, Value);
end;

end.
