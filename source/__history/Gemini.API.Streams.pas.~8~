unit Gemini.API.Streams;

interface

uses
  System.SysUtils, System.Classes, System.SyncObjs;

type
  TLockedMemoryStream = class(TMemoryStream)
  private
    FLock: TCriticalSection;
  public
    constructor Create;
    destructor Destroy; override;

    /// <summary>
    /// Writes <paramref name="Count"/> bytes from <paramref name="Buffer"/> into the stream
    /// in a thread-safe manner.
    /// </summary>
    /// <param name="Buffer">
    /// The source memory block containing the bytes to write.
    /// </param>
    /// <param name="Count">
    /// The number of bytes to write from <paramref name="Buffer"/>.
    /// </param>
    /// <returns>
    /// The number of bytes actually written.
    /// </returns>
    /// <remarks>
    /// This override serializes access to the underlying <see cref="TMemoryStream"/> storage
    /// using a critical section. It is designed to prevent races between concurrent writers
    /// and readers (such as <see cref="ExtractDelta"/>) that could otherwise occur when the
    /// stream grows and its internal buffer is reallocated.
    /// </remarks>
    function Write(const Buffer; Count: Longint): Longint; override;

    function ExtractDelta(var Offset: Int64; out Bytes: TBytes): Boolean;
  end;

implementation

{ TLockedMemoryStream }

constructor TLockedMemoryStream.Create;
begin
  inherited Create;
  FLock := TCriticalSection.Create;
end;

destructor TLockedMemoryStream.Destroy;
begin
  FLock.Free;
  inherited;
end;

function TLockedMemoryStream.Write(const Buffer; Count: Longint): Longint;
begin
  FLock.Enter;
  try
    Result := inherited Write(Buffer, Count);
  finally
    FLock.Leave;
  end;
end;

function TLockedMemoryStream.ExtractDelta(var Offset: Int64; out Bytes: TBytes): Boolean;
var
  NewCount: Int64;
begin
  Result := False;
  SetLength(Bytes, 0);

  FLock.Enter;
  try
    if (Offset < 0) or (Offset > Size) then
      Exit;

    NewCount := Size - Offset;
    if NewCount <= 0 then
      Exit;

    SetLength(Bytes, NewCount);
    Move(PByte(Memory)[NativeInt(Offset)], Bytes[0], NewCount);
    Inc(Offset, NewCount);
    Result := True;
  finally
    FLock.Leave;
  end;
end;


end.
